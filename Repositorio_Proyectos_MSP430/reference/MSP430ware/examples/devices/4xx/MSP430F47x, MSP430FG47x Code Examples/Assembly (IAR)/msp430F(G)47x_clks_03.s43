;*****************************************************************************
; MSP430F(G)47x Demo - FLL+, Output 32kHz XTAL + HF XTAL + Internal DCO
;
;  Description: This program demonstrates using an external 32kHz crystal to
;  supply the internal ACLK, and using a high speed crystal or resonator to
;  supply SMCLK. The MLCK for the CPU is supplied by the DCO. The 32kHz
;  crystal connects to pins Xin and Xout. The high frequency crystal or
;  resonator connects to pins XT2in and XT2out. The DCO clock is generated
;  internally and calibrated by the FLL+ from the 32kHz crystal. The
;  resulting ACLK is brought out pin P1.5, SMCLK is brought out on P1.4,
;  and MCLK is brought out on pin P1.1.
;  ACLK = LFXT1 = 32768Hz, MCLK = SMCLK = default DCO = 32 x ACLK = 1048576Hz
;  //* An external watch crystal between XIN & XOUT is required for ACLK *//
;  //* External matching capacitors must be added for the high *//
;  //* speed crystal or resonator as required.                 *//
;
;                MSP430F(G)47x
;            -----------------
;        /|\ |              XIN|-
;         |  |                 | 32kHz
;         ---|RST          XOUT|-
;            |                 |
;            |                 |
;            |            XT2IN|-
;            |                 | HF XTAL or Resonator (add capacitors)
;            |           XT2OUT|-
;            |                 |
;            |                 |
;            |        P1.5/ACLK|--> ACLK = 32768 Hz
;            |                 |
;            |       P1.4/SMCLK|--> SMCLK = High Freq XTAL or Resonator
;            |                 |
;            |        P1.1/MCLK|--> MCLK = 1048576 Hz (Default DCO)
;            |                 |
;            |                 |
;
;   M.Seamen
;   Texas Instruments Inc.
;   September 2008
;   Built with IAR Embedded Workbench Version: 4.11A
;*****************************************************************************
#include  <msp430xG47x.h>
;-------------------------------------------------------------------------------
            ORG     01100h                  ; Program Start
;-------------------------------------------------------------------------------
RESET       mov.w   #0A00h,SP               ; Initialize stack pointer
SetupWDT    mov.w   #WDTPW+WDTHOLD,&WDTCTL  ; Stop WDT
SetupFLL2   bis.b   #XCAP18PF,&FLL_CTL0     ; Set load capacitance for 32k xtal
SetupHF     bic.b   #XT2OFF,&FLL_CTL1       ; Clear bit = high freq xtal on
ClearFlag   bic.b   #OFIFG,&IFG1            ; Clear osc fault flag
            mov     #0F000h,R15             ; Move delay time to register 15
HF_Wait     dec     R15                     ; Delay for xtal to start, FLL lock
            jnz     HF_Wait		    ; Loop if delay not finished
            bit.b   #OFIFG,&IFG1            ; Test osc fault flag
            jnz     ClearFlag               ; If not loop again
SwitchHF    bis.b   #SELS,&FLL_CTL1         ; Is reset so switch SMCLK = HF xtal
                                            ;
SetupPorts  bis.b   #032h,&P1DIR            ; P1.1, P1.4 & P1.5 to outputs
            bis.b   #032h,&P1SEL            ; P1.1, P1.4 & P1.5 functions
            bis.b   #002h,&P1SEL2           ; P1.1 to output MCLK
                                            ; MCLK, SMCLK & ACLK

Mainloop    jmp     Mainloop                ; Loop with CPU running
                                            ;
;-------------------------------------------------------------------------------
;           Interrupt Vectors
;-------------------------------------------------------------------------------
            ORG     0FFFEh                  ; RESET Vector
            DW      RESET                   ;
            END
