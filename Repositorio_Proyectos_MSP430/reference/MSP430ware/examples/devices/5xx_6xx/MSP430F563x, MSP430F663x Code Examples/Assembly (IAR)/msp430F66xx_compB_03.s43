;******************************************************************************
; MSP430x66x Demo - COMPB interrupt capability; Vcompare is compared against 
;                    internal 1.5V reference
;
; Description: Use CompB and internal reference to determine if input'Vcompare'
; is high or low.  For the first time, when Vcompare exceeds the 1.5V internal
; reference, CBIFG is set and device enters the CompB ISR. In the ISR CBIES is
; toggled such that the next time when Vcompare is less than 1.5V reference, 
; CBIFG is set.
; LED is toggled inside the CompB ISR
;                                                   
;                 MSP430x66x
;             ------------------                        
;         /|\|                  |                       
;          | |                  |                       
;          --|RST      P6.0/CB0 |<--Vcompare            
;            |                  |                                         
;            |            P1.0  |--> LED 'ON'(Vcompare>1.5V); 'OFF'(Vcompare<1.5V)
;            |                  | 
;   Priya Thanigai
;   Texas Instruments Inc.
;   March 2010
;   Built with IAR Embedded Workbench Version: 4.20
;******************************************************************************
#include <msp430f6638.h>


;-------------------------------------------------------------------------------
            RSEG    CSTACK                  ; Define stack segment
;-------------------------------------------------------------------------------
            RSEG    CODE                    ; Assemble to Flash memory
;-------------------------------------------------------------------------------
RESET       mov.w   #SFE(CSTACK),SP         ; Initialize stackpointer

StopWDT     mov.w   #WDTPW+WDTHOLD,&WDTCTL  ; Stop WDT

SetupPort   bis.b   #BIT0,&P1DIR
            bis.b   #BIT0,&P3DIR            ; P3.0 output
            bis.b   #BIT0,&P3SEL            ; P3.0 option select
            
            bis.w   #CBIPEN+CBIPSEL_0,&CBCTL0; Enable V+, input channel CB1
            bis.w   #CBPWRMD_1,&CBCTL1      ; normal power mode
            bis.w   #CBRSEL,&CBCTL2         ; Vref to -ve terminal
            bis.w   #CBRS_3+CBREFL_1,&CBCTL2; R ladder off, 1.2V
            
            bis.w   #BIT0,&CBCTL3           ; Input buffer disable P6.0
            bic.w   #CBIIFG+CBIFG,&CBINT
            bis.w   #CBIE,&CBINT
            bis.w   #CBON,&CBCTL1
            
; Delay for reference settle = 75us
            mov.w   #01ffh,R15
delay_L1    dec.w   R15
            jnz     delay_L1
            
            bis.w   #LPM4+GIE,SR            ; Enter low power mode
            nop

COMPB_ISR 

            xor.w   #CBIES,&CBCTL1          ; toggle interrupt edge
            bic.w   #CBIFG,&CBINT           ; clear interrupt flag
            xor.b   #0x01,&P1OUT            ; toggle LED
            reti
;-------------------------------------------------------------------------------
            COMMON  INTVEC                  ; Interrupt Vectors
;-------------------------------------------------------------------------------
            ORG     RESET_VECTOR                ; POR, ext. Reset
            DW      RESET
            ORG     COMP_B_VECTOR               ; COMP B isr vector
            DW      COMPB_ISR            
            END
